plugins {
    // Processes Lombok annotations during build
    id 'io.franzbecker.gradle-lombok' version '3.0.0'

    // Enables download tasks
    id "de.undercouch.download" version "3.4.3"

    // Adds dependencyUpdates task
    id 'com.github.ben-manes.versions' version '0.21.0'
}

apply plugin: 'java'

group = 'CDDBaselineJava'
version = '0.3'

description = """"""

def gradleDependencyVersion = '5.4.1'

wrapper {
    gradleVersion = gradleDependencyVersion
}

lombok {
    version = '1.18.6'
    sha256 = ''
}

sourceCompatibility = 1.8
targetCompatibility = 1.8
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()
}

def gsonVersion = '2.8.5'
def guiceVersion = '4.2.2'
def slf4jVersion = '1.7.25'
def jacksonVersion = '2.9.8'
def httpClientVersion = '4.5.8'
def awsLambdaJavaCoreVersion = '1.2.0'
def commonsIoVersion = '2.6'
def awsSdkVersion = '1.11.546'

def libsDir = file("$buildDir/libs")
def tempDir = file("$buildDir/temp")
delete libsDir
libsDir.mkdirs()
delete tempDir
tempDir.mkdirs()

def javaSdkTarGz = 'aws-greengrass-core-sdk-java-1.3.1.tar.gz'
def javaSdkJar = 'GreengrassJavaSDK-1.3.1.jar'

task fetchGreengrassJavaSdk(type: Download) {
    src "https://d1onfpft10uf5o.cloudfront.net/greengrass-sdk/downloads/java/8/$javaSdkTarGz"
    dest new File(libsDir, javaSdkTarGz)
    overwrite false
}

task extractJavaSdk(type: Copy, dependsOn: fetchGreengrassJavaSdk) {
    from tarTree(fetchGreengrassJavaSdk.dest)
    into tempDir
}

task downloadJavaSdk(type: Copy, dependsOn: extractJavaSdk) {
    from "$tempDir/aws-greengrass-core-sdk-java/sdk/$javaSdkJar"
    into "$libsDir"
}

compileJava.dependsOn downloadJavaSdk

dependencies {
    compile fileTree(dir: "$libsDir", include: ["*.jar"])

    compile "com.google.code.gson:gson:$gsonVersion"
    compile "com.google.inject:guice:$guiceVersion"
    compile "org.slf4j:slf4j-jdk14:$slf4jVersion"
    compile "com.fasterxml.jackson.core:jackson-core:$jacksonVersion"
    compile "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
    compile "org.apache.httpcomponents:httpclient:$httpClientVersion"
    compile "com.amazonaws:aws-lambda-java-core:$awsLambdaJavaCoreVersion"
    compile "com.amazonaws:aws-java-sdk-core:$awsSdkVersion"
    compile "com.amazonaws:aws-java-sdk-greengrass:$awsSdkVersion"
    compile "commons-io:commons-io:$commonsIoVersion"
}
