plugins {
    // Enables download tasks
    id "de.undercouch.download" version "3.4.3"

    // Creates fat JAR
    id 'com.github.johnrengelman.shadow' version '5.0.0'

    // Adds dependencyUpdates task
    id 'com.github.ben-manes.versions' version '0.21.0'
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'application'
apply plugin: 'idea'

// Required by shadow but not necessary
mainClassName = 'not-necessary'

group = 'com.awslabs.aws.iot.greengrass.cdd'
version = '0.3.8'

description = """"""

def gradleDependencyVersion = '5.4.1'

wrapper {
    gradleVersion = gradleDependencyVersion
}

sourceCompatibility = 1.8
targetCompatibility = 1.8
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()
    maven { url "https://plugins.gradle.org/m2/" }
}

def gsonVersion = '2.8.5'
def guiceVersion = '4.2.2'
def slf4jVersion = '1.7.26'
def jacksonVersion = '2.9.9'
def httpClientVersion = '4.5.8'
def awsLambdaJavaCoreVersion = '1.2.0'
def commonsIoVersion = '2.6'
def awsSdkVersion = '1.11.569'
def immutablesValueVersion = '2.7.5'

def libsDir = file("$buildDir/libs")
def tempDir = file("$buildDir/temp")
delete libsDir
libsDir.mkdirs()
delete tempDir
tempDir.mkdirs()

def javaSdkTarGz = 'aws-greengrass-core-sdk-java-1.3.1.tar.gz'
def javaSdkJar = 'GreengrassJavaSDK-1.3.1.jar'

task fetchJavaSdk(type: Download) {
    src "https://d1onfpft10uf5o.cloudfront.net/greengrass-sdk/downloads/java/8/$javaSdkTarGz"
    dest new File(libsDir, javaSdkTarGz)
    overwrite false
}

task extractJavaSdk(type: Copy, dependsOn: fetchJavaSdk) {
    from tarTree(fetchJavaSdk.dest)
    into tempDir
}

task extractGreengrassJarFromJavaSdk(type: Copy, dependsOn: extractJavaSdk) {
    from "$tempDir/aws-greengrass-core-sdk-java/sdk/$javaSdkJar"
    into "$libsDir"
}

task unpackGreengrassJar(type: Copy, dependsOn: extractGreengrassJarFromJavaSdk) {
    from zipTree("$libsDir/$javaSdkJar")
    into "$buildDir/classes/java/main"
}

compileJava.dependsOn unpackGreengrassJar

// Workaround for https://github.com/gradle/gradle/issues/4956
sourceSets.configureEach { sourceSet ->
    tasks.named(sourceSet.compileJavaTaskName).configure {
        options.annotationProcessorGeneratedSourcesDirectory = file("$buildDir/generated/sources/annotationProcessor/java/${sourceSet.name}")
    }
}

// Workaround for https://youtrack.jetbrains.com/issue/IDEA-182577
idea {
    module {
        sourceDirs += compileJava.options.annotationProcessorGeneratedSourcesDirectory
        generatedSourceDirs += compileJava.options.annotationProcessorGeneratedSourcesDirectory
        testSourceDirs += compileTestJava.options.annotationProcessorGeneratedSourcesDirectory
        generatedSourceDirs += compileTestJava.options.annotationProcessorGeneratedSourcesDirectory
    }
}

dependencies {
    annotationProcessor "org.immutables:value:$immutablesValueVersion"
    compile "org.immutables:value:$immutablesValueVersion"

    compile fileTree(dir: "$libsDir", include: ["*.jar"])

    compile "com.google.code.gson:gson:$gsonVersion"
    compile "com.google.inject:guice:$guiceVersion"
    compile "org.slf4j:slf4j-jdk14:$slf4jVersion"
    compile "com.fasterxml.jackson.core:jackson-core:$jacksonVersion"
    compile "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
    compile "org.apache.httpcomponents:httpclient:$httpClientVersion"
    compile "com.amazonaws:aws-lambda-java-core:$awsLambdaJavaCoreVersion"
    compile "com.amazonaws:aws-java-sdk-core:$awsSdkVersion"
    compile "com.amazonaws:aws-java-sdk-greengrass:$awsSdkVersion"
    compile "commons-io:commons-io:$commonsIoVersion"
}
