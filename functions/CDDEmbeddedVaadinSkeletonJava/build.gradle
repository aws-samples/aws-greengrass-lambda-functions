plugins {
    id 'java'
    id 'application'
    id 'idea'

    // Creates fat JAR
    id 'com.github.johnrengelman.shadow' version '5.2.0'

    // Adds dependencyUpdates task
    id 'com.github.ben-manes.versions' version '0.28.0'
}

distZip.enabled = shadowDistZip.enabled = false
distTar.enabled = shadowDistTar.enabled = false

// Required by shadow but not necessary
mainClassName = 'not-necessary'

group = 'CDDEmbeddedVaadinSkeletonJava'
version = '1.0-SNAPSHOT'

description = """"""

sourceCompatibility = 1.8
targetCompatibility = 1.8

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

def gradleDependencyVersion = '6.2'

wrapper {
    gradleVersion = gradleDependencyVersion
    distributionType = Wrapper.DistributionType.ALL
}

repositories {
    mavenCentral()
    maven { url "http://maven.vaadin.com/vaadin-addons" }
    maven { url "https://plugins.gradle.org/m2/" }
    maven { url 'https://jitpack.io' }
}

def cddVersion = '0.8.51'
def gsonVersion = '2.8.6'
def slf4jVersion = '1.7.30'
def awsSdkVersion = '1.11.718'
def jacksonVersion = '2.10.2'
def awsLambdaJavaCoreVersion = '1.2.0'
def immutablesValueVersion = '2.8.3'

def jettyVersion = '9.4.26.v20200117'
def vaadinVersion = '8.10.1'
def daggerVersion = '2.26'

dependencies {
    annotationProcessor "org.immutables:value:$immutablesValueVersion"
    compile "org.immutables:value:$immutablesValueVersion"

    compile "com.github.aws-samples:aws-greengrass-lambda-functions:$cddVersion"

    // Dagger code generation
    annotationProcessor "com.google.dagger:dagger-compiler:$daggerVersion"

    // Dependency injection with Dagger
    compile "com.google.dagger:dagger:$daggerVersion"

    compile "com.google.code.gson:gson:$gsonVersion"
    compile "org.slf4j:slf4j-jdk14:$slf4jVersion"
    compile "com.amazonaws:aws-java-sdk-core:$awsSdkVersion"
    compile "com.fasterxml.jackson.core:jackson-core:$jacksonVersion"
    compile "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
    compile "com.amazonaws:aws-lambda-java-core:$awsLambdaJavaCoreVersion"

    compile "org.eclipse.jetty:jetty-servlet:$jettyVersion"
    compile "org.eclipse.jetty:jetty-continuation:$jettyVersion"
    compile "org.eclipse.jetty.websocket:websocket-server:$jettyVersion"

    compile "com.vaadin:vaadin-server:$vaadinVersion"
    compile "com.vaadin:vaadin-push:$vaadinVersion"
    compile "com.vaadin:vaadin-client-compiled:$vaadinVersion"
    compile "com.vaadin:vaadin-themes:$vaadinVersion"
}

task depsize  {
    doLast {
        final formatStr = "%,10.2f"
        final conf = configurations.default
        final size = conf.collect { it.length() / (1024 * 1024) }.sum()
        final out = new StringBuffer()
        out << 'Total dependencies size:'.padRight(45)
        out << "${String.format(formatStr, size)} Mb\n\n"
        conf.sort { -it.length() }
            .each {
                out << "${it.name}".padRight(45)
                out << "${String.format(formatStr, (it.length() / 1024))} kb\n"
            }
        println(out)
    }
}
